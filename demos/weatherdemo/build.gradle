if (!config.isAssembling) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion config.compileSdkVersion
    buildToolsVersion config.buildToolsVersion

    if (config.isAssembling) {
        resourcePrefix "weatherdemo_"
    }

    defaultConfig {
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode config.versionCode
        versionName config.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        if (!config.isAssembling) {
            applicationId "jason.jan.demos.weatherdemo"
        } else {
            //集成模式下Arouter的配置，用于组件间通信的实现
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = [moduleName: project.getName()]
                }
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "boolean", "USE_CANARY", "true"
            //minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "boolean", "USE_CANARY", "false"
            //minifyEnabled true
            //shrinkResources true
            //zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            //控制两种模式下的资源和代码配置情况
            if (!config.isAssembling) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件,就是说没必要打包
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    //DataBinding
    dataBinding {
        enabled true
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {

    api project(":common:commonlib")

    implementation google.appcompat
    implementation google.recyclerview
    implementation google.material
    implementation google.constraintlayout
    implementation google.kotlin_stdlib_jdk7
    implementation google.kotlin_stdlib
    implementation google.dagger2
    kapt google.dagger2_compiler
    kapt google.dagger2_android_processor
    implementation google.room_runtime
    kapt google.room_compiler
    implementation google.room_rxjava2
    implementation google.room_common

    implementation common.supertextview
    implementation common.material_searchview

    debugImplementation test.canary_debug
    releaseImplementation test.canary_release
}
